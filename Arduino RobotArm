#include <Servo.h>

Servo servoBase;
Servo servoArm1;
Servo servoArm2;
Servo servoClaw;

bool clawOpen = false;
bool lastButtonState1 = HIGH;
bool lastButtonState2 = HIGH;

bool emergencyStop = false;
bool lastEmergencyButtonState = HIGH;

#define MAX_RECORD 200  // 降低录制帧数，避免内存溢出

byte recordBase[MAX_RECORD];
byte recordArm1[MAX_RECORD];
byte recordArm2[MAX_RECORD];
byte recordClaw[MAX_RECORD];
int recordIndex = 0;

bool isRecording = false;
bool lastRecordButtonState = HIGH;

bool isPlaying = false;
bool lastPlayButtonState = HIGH;
int playIndex = 0;

void setup() {
  servoBase.attach(11);
  servoArm1.attach(9);
  servoArm2.attach(10);
  servoClaw.attach(6);

  pinMode(2, INPUT_PULLUP);  // 爪子按钮1
  pinMode(4, INPUT_PULLUP);  // 爪子按钮2
  pinMode(5, INPUT_PULLUP);  // 急停按钮
  pinMode(3, INPUT_PULLUP);  // 录制按钮
  pinMode(7, INPUT_PULLUP);  // 回放按钮

  Serial.begin(9600);
}

void loop() {
  // 回放动作逻辑
  if (isPlaying) {
    if (playIndex < recordIndex) {
      servoBase.write(recordBase[playIndex]);
      servoArm1.write(recordArm1[playIndex]);
      servoArm2.write(recordArm2[playIndex]);
      servoClaw.write(recordClaw[playIndex]);
      playIndex++;
      delay(20);
    } else {
      isPlaying = false;
      Serial.println("播放结束！");
    }
    return;
  }

  // 急停处理
  bool currentEmergencyButtonState = digitalRead(5);
  if (currentEmergencyButtonState == LOW && lastEmergencyButtonState == HIGH) {
    emergencyStop = !emergencyStop;
    Serial.print("急停状态: ");
    Serial.println(emergencyStop ? "ON" : "OFF");
    delay(70);
  }
  lastEmergencyButtonState = currentEmergencyButtonState;

  if (emergencyStop) return;

  // 录制按钮
  bool currentRecordButtonState = digitalRead(3);
  if (currentRecordButtonState == LOW && lastRecordButtonState == HIGH) {
    isRecording = !isRecording;
    if (isRecording) {
      recordIndex = 0;
      Serial.println("开始录制动作...");
    } else {
      Serial.print("录制结束，总帧数：");
      Serial.println(recordIndex);
    }
    delay(70);
  }
  lastRecordButtonState = currentRecordButtonState;

  // 回放按钮处理
  bool currentPlayButtonState = digitalRead(7);
  if (currentPlayButtonState == LOW && lastPlayButtonState == HIGH) {
    if (!isRecording && recordIndex > 0) {
      isPlaying = true;
      playIndex = 0;
      Serial.println("开始播放动作...");
    }
    delay(70);
  }
  lastPlayButtonState = currentPlayButtonState;

  // 读取遥感值
  int joy1X = analogRead(A0); // 小臂
  int joy1Y = analogRead(A1); // 底盘
  int joy2X = analogRead(A3); // 大臂

  int angleBase = map(joy1Y, 0, 1023, 0, 300);    // 映射到0~180度，300度对Servo不适合
  int angleArm2 = map(joy1X, 0, 1023, 150, 0);    // 改成合理范围，且保证0-180内
  int angleArm1 = map(joy2X, 0, 1023, 0, 180);

  servoBase.write(angleBase);
  servoArm1.write(angleArm1);
  servoArm2.write(angleArm2);

  // 爪子按钮控制
  bool currentButton1 = digitalRead(2);
  bool currentButton2 = digitalRead(4);
  if ((currentButton1 == LOW && lastButtonState1 == HIGH) ||
      (currentButton2 == LOW && lastButtonState2 == HIGH)) {
    clawOpen = !clawOpen;
    if (clawOpen) {
      servoClaw.write(30);
    } else {
      servoClaw.write(90);
    }
    delay(70);
  }
  lastButtonState1 = currentButton1;
  lastButtonState2 = currentButton2;

  // 录制当前角度帧
  if (isRecording && recordIndex < MAX_RECORD) {
    recordBase[recordIndex] = angleBase;
    recordArm1[recordIndex] = angleArm1;
    recordArm2[recordIndex] = angleArm2;
    recordClaw[recordIndex] = clawOpen ? 30 : 90;
    recordIndex++;
  }

  delay(20);
}
